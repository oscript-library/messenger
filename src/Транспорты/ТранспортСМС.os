
///////////////////////////////////////////////////////////////////////////////////////////////
//
// Модуль транспорта отправки сообщений СМС
//
///////////////////////////////////////////////////////////////////////////////////////////////

Перем ПараметрыАвторизации;	// хранит структуру авторизации
Перем ОписаниеПротокола;	// хранит структуру описания протокола

Перем ДоступныеОператорыSMS;

///////////////////////////////////////////////////////////////////////////////////////////////
// Стандартный интерфейс
///////////////////////////////////////////////////////////////////////////////////////////////

// Протокол
//	Метод возвращает описание используемого протокола
//
// Возвращаемое значение:
//	Структура - Описание протокола
//		{
//			Имя 			- Строка - Системное имя транспорта
//			Представление 	- Строка - пользовательское представление транспорта
//			Описание		- Строка - Строковое описание транспорта
//			Операторы		- Структура - Возможные операторы транспорта
//		}
//
Функция Протокол() Экспорт
	
	Если ОписаниеПротокола = Неопределено Тогда

		ОписаниеПротокола = Новый Структура("Имя, Представление, Описание, Операторы", "sms", "СМС", "Отправка СМС", ДоступныеОператоры());

	КонецЕсли;

	Возврат ОписаниеПротокола;

КонецФункции // Протокол()

// Инициализация
//	Инициализация параметров транспорта
//
// Параметры:
//  ПараметрыИнициализации - Структура - набор параметров инициализации
//
Процедура Инициализация(ПараметрыИнициализации) Экспорт
	
	Если ПараметрыИнициализации = Неопределено Тогда

		Сообщить(СтрШаблон("Для инициализации транспорта %1 необходимо передавать в параметрах: ", Протокол().Представление));
		Сообщить(" - Логин");
		Сообщить(" - Пароль");
		Сообщить(" - Подпись");
		Сообщить(" - КодОператора");

		ВызватьИсключение СтрШаблон("Инициализация транспорта %1 невыполнена", Протокол().Представление);

	КонецЕсли;

	Логин = ПараметрыИнициализации.Логин;
	Пароль = ПараметрыИнициализации.Пароль;
	Подпись = ПараметрыИнициализации.Подпись;
	КодОператора = ПараметрыИнициализации.КодОператора;

	Если КодОператора = ДоступныеОператоры().smsbliss Тогда
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		
		ПараметрыАвторизации = Новый Структура("ШаблонТелаЗапроса, Логин, Пароль, Подпись", ПолучитьШаблонТелаЗапросаSMSBliss(), Логин, Пароль, Подпись);
		ПараметрыАвторизации.Вставить("ИмяСервера", "json.gate.smsbliss.ru");
		ПараметрыАвторизации.Вставить("URL", "send");
		ПараметрыАвторизации.Вставить("Заголовки", Заголовки);
		
	ИначеЕсли КодОператора = ДоступныеОператоры().infobip Тогда
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
					
		ПараметрыАвторизации = Новый Структура("ШаблонТелаЗапроса, Логин, Пароль, Подпись", ПолучитьШаблонТелаЗапросаInfobip(), Логин, Пароль, Подпись);
		ПараметрыАвторизации.Вставить("ИмяСервера", "api.infobip.com");
		ПараметрыАвторизации.Вставить("URL", "api/v3/sendsms/json");
		ПараметрыАвторизации.Вставить("Заголовки", Заголовки);
		
	ИначеЕсли КодОператора = ДоступныеОператоры().sms4b Тогда
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Accept-Encoding", "gzip,deflate");
		Заголовки.Вставить("Content-Type", "text/xml;charset=UTF-8");
		Заголовки.Вставить("SOAPAction", "SMS4B/SendSMS");
		
		ПараметрыАвторизации = Новый Структура("ШаблонТелаЗапроса, Логин, Пароль, Подпись", ПолучитьШаблонТелаЗапросаSms4b(), Логин, Пароль, Подпись);
		ПараметрыАвторизации.Вставить("ИмяСервера", "https://sms4b.ru");
		ПараметрыАвторизации.Вставить("URL", "ws/sms.asmx");
		ПараметрыАвторизации.Вставить("Заголовки", Заголовки);
		
	Иначе
		
		ВызватьИсключение "Неизвестный код оператора: " + КодОператора;
		
	КонецЕсли;

КонецПроцедуры // Инициализация()

// ОтправитьСообщение
//	Метод отправки сообщения
//
// Параметры:
//	Адресат					- Строка	- Адресат сообщения
//	Сообщение				- Строка	- Текст отправляемого сообщения
//	ДополнительныеПараметры	- Структура	- Набор дополнительных параметров
//
Процедура ОтправитьСообщение(Адресат, Сообщение, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПараметрыАвторизации = Неопределено Тогда

		ВызватьИсключение СтрШаблон("Для отправки сообщения необходимо выполнить инициализацию транспорта %1", Протокол().Представление);

	КонецЕсли;

	URL = ПараметрыАвторизации.URL;
	ИмяСервера = ПараметрыАвторизации.ИмяСервера;
			
	ТелоЗапроса = СтрШаблон(ПараметрыАвторизации.ШаблонТелаЗапроса, 
							ПараметрыАвторизации.Логин,
							ПараметрыАвторизации.Пароль,
							Адресат,
							Сообщение,
							ПараметрыАвторизации.Подпись); 
				
	HTTPЗапрос = Новый HTTPЗапрос(ПараметрыАвторизации.URL, ПараметрыАвторизации.Заголовки);
	HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса);
		
	HTTP = Новый HTTPСоединение(ИмяСервера);
	Ответ = HTTP.ОтправитьДляОбработки(HTTPЗапрос);
	
КонецПроцедуры // ОтправитьСообщение()

///////////////////////////////////////////////////////////////////////////////////////////////
// Методы реализации
///////////////////////////////////////////////////////////////////////////////////////////////

Функция ДоступныеОператоры()
	
	Если ДоступныеОператорыSMS = Неопределено Тогда

		ДоступныеОператорыSMS = Новый Структура("smsbliss, infobip, sms4b", "smsbliss", "infobip", "sms4b");

	КонецЕсли;
	
	Возврат ДоступныеОператорыSMS;

КонецФункции

Функция ПолучитьШаблонТелаЗапросаSMSBliss()
	
	Возврат
		"{ 
		|""login"": ""%1"",
		|""password"": ""%2"", 
		|""messages"" :[
		|	{
		|	""clientId"": 0,
		|	""phone"": ""%3"",
		|	""text"": ""%4"",
		|	""sender"": ""%5""
		|	}]
		|}";

КонецФункции
	
Функция ПолучитьШаблонТелаЗапросаInfobip()
	
	Возврат
		"{ 
		|""authentication"": 
		|	{
		|	""username"": ""%1"", 
		|	""password"": ""%2""
		|	}, 
		|""messages"" :[
		|	{
		|	""sender"": ""%5"",
		|	""text"": ""%4"",
		|	""type"": ""longSMS"",
		|	""datacoding"": ""8"",
		|	""recipients"": [{
		|		""gsm"": ""%3""}]
		|	}
		|]
		|}";
	
КонецФункции
	
Функция ПолучитьШаблонТелаЗапросаSms4b()
	
	 Возврат 
		"<soapenv:Envelope xmlns:soapenv=" + Символ(34) + "http://schemas.xmlsoap.org/soap/envelope/" + Символ(34) + " xmlns:sms="+Символ(34)+"SMS4B"+Символ(34)+">
		|<soapenv:Header/>
		|<soapenv:Body>
		|<sms:SendSMS>
		|<sms:Login>%1</sms:Login>
		|<sms:Password>%2</sms:Password>
		|<sms:Source>%5</sms:Source>
		|<sms:Phone>%3</sms:Phone>
		|<sms:Text>%4</sms:Text>
		|</sms:SendSMS>
		|</soapenv:Body>
		|</soapenv:Envelope>";
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

ПараметрыАвторизации = Неопределено;
