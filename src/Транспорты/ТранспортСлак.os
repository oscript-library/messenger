
///////////////////////////////////////////////////////////////////////////////////////////////
//
// Модуль транспорта отправки сообщений slack
//
///////////////////////////////////////////////////////////////////////////////////////////////

Перем ПараметрыАвторизации;	// хранит структуру авторизации
Перем ОписаниеПротокола;	// хранит структуру описания протокола

///////////////////////////////////////////////////////////////////////////////////////////////
// Стандартный интерфейс
///////////////////////////////////////////////////////////////////////////////////////////////

// Протокол
//	Метод возвращает описание используемого протокола
//
// Возвращаемое значение:
//	Структура - Описание протокола
//		{
//			Имя 			- Строка - Системное имя транспорта
//			Представление 	- Строка - пользовательское представление транспорта
//			Описание		- Строка - Строковое описание транспорта
//			Операторы		- Структура - Возможные операторы транспорта
//		}
//
Функция Протокол() Экспорт
	
	Если ОписаниеПротокола = Неопределено Тогда

		ОписаниеПротокола = Новый Структура("Имя, Представление, Описание, Операторы", "slack", "slack", "Отправка сообщений в каналы slack", Неопределено);

	КонецЕсли;

	Возврат ОписаниеПротокола;

КонецФункции // Протокол()

// Инициализация
//	Инициализация параметров транспорта
//
// Параметры:
//  ПараметрыИнициализации - Структура - набор параметров инициализации
//
Процедура Инициализация(ПараметрыИнициализации) Экспорт
	
	Если ПараметрыИнициализации = Неопределено Тогда

		Сообщить(СтрШаблон("Для инициализации транспорта %1 необходимо передавать в параметрах: ", Протокол().Представление));
		Сообщить(" - Логин");
		Сообщить(" - Ключ");
		
		ВызватьИсключение СтрШаблон("Инициализация транспорта %1 невыполнена", Протокол().Представление);

	КонецЕсли;
	
	ПараметрыАвторизации = Новый Структура("Логин, Ключ", ПараметрыИнициализации.Логин, ПараметрыИнициализации.Пароль);
	
КонецПроцедуры // Инициализация()

// ОтправитьСообщение
//	Метод отправки сообщения
//
// Параметры:
//	Адресат					- Строка	- Адресат сообщения
//	Сообщение				- Строка	- Текст отправляемого сообщения
//	ДополнительныеПараметры	- Структура	- Набор дополнительных параметров
//
Процедура ОтправитьСообщение(Адресат, Сообщение, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ПараметрыАвторизации = Неопределено Тогда

		ВызватьИсключение СтрШаблон("Для отправки сообщения необходимо выполнить инициализацию транспорта %1", Протокол().Представление);

	КонецЕсли;

	ИмяСервера = "slack.com";
	Прокси = Новый ИнтернетПрокси(ИСТИНА);
	
	URL = "api/chat.postMessage?channel=" 
		+ Адресат 
		+ "&text=" + СформироватьТекстСообщения(ДополнительныеПараметры.ТипСообщения, Сообщение) 
		+ "&as_user=" + ПараметрыАвторизации.Логин + "&token=" + ПараметрыАвторизации.Ключ;
	

	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = URL;
	
	HTTP = Новый HTTPСоединение(ИмяСервера,,,, Прокси);
	Ответ = HTTP.Получить(HTTPЗапрос);

КонецПроцедуры // ОтправитьСообщение()

///////////////////////////////////////////////////////////////////////////////////////////////
// Методы реализации
///////////////////////////////////////////////////////////////////////////////////////////////

Функция СформироватьТекстСообщения(ТипСообщения, ТекстСообщения)
	
	Сообщение = ПолучитьИконкуТипаСообщения(ТипСообщения) + " " + КодироватьСтроку(ТекстСообщения, СпособКодированияСтроки.КодировкаURL);
	Возврат Сообщение;
	
КонецФункции 

Функция ПолучитьИконкуТипаСообщения(ТипСообщения)
	
	Иконка = ТипСообщения;
	Если СтрСравнить(ТипСообщения, "Ошибка") = 0 Тогда
		
		Иконка = ":no_entry:";
		
	ИначеЕсли СтрСравнить(ТипСообщения, "Информация") = 0 Тогда
			
		Иконка = ":speech_balloon:";
			
	ИначеЕсли СтрСравнить(ТипСообщения, "Предупреждение") = 0 Тогда
		
		Иконка = ":warning:";  
		
	КонецЕсли;
		
	Возврат Иконка;
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////////////////////

ПараметрыАвторизации = Неопределено;
